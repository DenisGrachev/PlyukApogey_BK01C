
/*
Player for Fast PSG Packer for compression levels [4..5]
Source for sjasm cross-assembler.
Source code is based on psndcj/tbk player and bfox/tmk player.
Modified by physic 8.12.2021.

8080 Version by Denis Grachev

Max time is 930t for compression level 4 (recomended), 1032t for compression level 5
Player size is increased from 348 to 543 bytes.

11hhhhhh llllllll nnnnnnnn	3	CALL_N - вызов с возвратом для проигрывания (nnnnnnnn + 1) значений по адресу 11hhhhhh llllllll
10hhhhhh llllllll			2	CALL_1 - вызов с возвратом для проигрывания одного значения по адресу 11hhhhhh llllllll
01MMMMMM mmmmmmmm			2+N	PSG2 проигрывание, где MMMMMM mmmmmmmm - инвертированная битовая маска регистров, далее следуют значения регистров.
							во 2-м байте также инвертирован порядок следования регистров (13..6)

001iiiii 					1	PSG2i проигрывание, где iiiii номер индексированной маски (0..31), далее следуют значения регистров
0001pppp					1	PAUSE16 - пауза pppp+1 (1..16)
0000hhhh vvvvvvvv			2	PSG1 проигрывание, 0000hhhh - номер регистра + 1, vvvvvvvv - значение
00001111					1	маркер оцончания трека
00000000 nnnnnnnn			2	PAUSE_N - пауза nnnnnnnn+1 фреймов (ничего не выводим в регистры)


Nested loops are fully supported at this version. Please make sure there is enough room for nested levels.
Packer shows max nested level. It need to increase MAX_NESTED_LEVEL variable if it not enough.
*/

;Если определана то играем в счётчик иначе в АУ
	DEFINE PLAY_VI53


VI1 = #EC03
VI2 = #EC02

;регистры АУ
AYREGS
;частоты
;A
AYR0: db 0
AYR1: db 0
;B
AYR2: db 0
AYR3: db 0
;C
AYR4: db 0
AYR5: db 0
;NOISE
AYR6: db 0
;MIXER MASKS
AYR7: db 0
;VOLUME A
AYR8: db 0
;VOLUME B
AYR9: db 0
;VOLUME C
AYR10: db 0
;OGIBA FREQ
AYR11: db 0
AYR12: db 0
;OGIBA MODE
AYR13: db 0


;AY PORTS - NOW FOR VECTOR-06C
AY_DATA = #14
AY_REG = #15

MAX_NESTED_LEVEL EQU 32 ;8

LD_HL_CODE	EQU 0x2A
JP_CODE		EQU 0xC3

			MACRO SAVE_POS
				ex	de,hl
				ld	hl, (trb_play+1)
				ld	(hl),e
				inc	l
				ld	(hl),d						
			ENDM

	IFNDEF PLAY_VI53
flushAY:	
	ld hl,AYREGS+13
	ld b,14
1:
	ld a,b : dec a :  out (AY_REG),a
	ld a,(hl) : out (AY_DATA),a
	dec hl
	dec b : jp nz,1b

	ret				

	ELSE		

vCH = #36 ;+#40
vPORT = #EC00 ;-1

;состояние каналов 0 - заглушен 1 - играет
;обновляем из регистра микшера АУ - R7 и из громкостей по каналам
vCH1: db 0
vCH2: db 0
vCH3: db 0

flushVI:

	ld a,(AYR7)
	or a : rra : push af
	jp nc,1f;канал включен
    ld a,(vCH1) : or a : jp z,2f
    ld a,vCH : ld (VI1),a;глушим канал
    xor a : ld (vCH1),a : jp 2f
1:
	ld a,1 : ld (vCH1),a
2:
	pop af

	or a : rra : push af
	jp nc,1f;канал включен
    ld a,(vCH2) : or a : jp z,2f
    ld a,vCH + #40 : ld (VI1),a;глушим канал
    xor a : ld (vCH2),a : jp 2f
1:
	ld a,1 : ld (vCH2),a
2:	

	pop af

	or a : rra
	jp nc,1f;канал включен
    ld a,(vCH3) : or a : jp z,2f
    ld a,vCH + #80 : ld (VI1),a;глушим канал
    xor a : ld (vCH3),a : jp 2f
1:
	ld a,1 : ld (vCH3),a
2:	


	;берём громкости
	ld a,(AYR8)
	;and 00001111b
	or a : jp nz,1f	
    ld a,(vCH1) : or a : jp z,2f
    ld a,vCH : ld (VI1),a;глушим канал
    xor a : ld (vCH1),a : jp 2f
1:
	ld a,1 : ld (vCH1),a
2:

	ld a,(AYR9)
	;and 00001111b
	or a : jp nz,1f	
    ld a,(vCH2) : or a : jp z,2f
    ld a,vCH+#40 : ld (VI1),a;глушим канал
    xor a : ld (vCH2),a : jp 2f
1:
	ld a,1 : ld (vCH2),a
2:

	ld a,(AYR10)
	;and 00001111b
	or a : jp nz,1f	
    ld a,(vCH3) : or a : jp z,2f
    ld a,vCH+#80 : ld (VI1),a;глушим канал
    xor a : ld (vCH3),a : jp 2f
1:
	ld a,1 : ld (vCH3),a
2:


	;пишем частоту
	ld a,(vCH1) : or a : jp z,1f;если канал включен суём частоту	
	ld hl,(AYR0) : call FreqAY_to_VI53
	ld a,l : ld (vPORT),a
	ld a,h : ld (vPORT),a
1:	

	ld a,(vCH2) : or a : jp z,1f;если канал включен суём частоту	
	ld hl,(AYR2) : call FreqAY_to_VI53
	ld a,l : ld (vPORT+1),a
	ld a,h : ld (vPORT+1),a
1:

	ld a,(vCH3) : or a : jp z,1f;если канал включен суём частоту	
	ld hl,(AYR4) : call FreqAY_to_VI53
	ld a,l : ld (vPORT+2),a
	ld a,h : ld (vPORT+2),a
1:

	ret

muteVI53:
 	ld a,vCH+#00 : ld (VI1),a;глушим канал
	ld a,vCH+#40 : ld (VI1),a;глушим канал
	ld a,vCH+#80 : ld (VI1),a;глушим канал
	ret

FreqAY_to_VI53:
        ;l-low h-high freq

		ld	a,00001111b
		and	h
		ld	h, a

		ld d,0 : ld e,h
Not0Freq:

	ex de,hl;save hl

	;calc 0.25
		ld	a,h
		or	a
		rra
		ld	h,a
		ld	a,l
		rra
		ld	l,a

		ld	a,h
		or	a
		rra
		ld	h,a
		ld	a,l
		rra
		ld	l,a
		
		ex de,hl

	;and 16
	add hl,hl
	add hl,hl
	add hl,hl
	add hl,hl

	add hl,de


		
/*		
		ld	a,00001111b
		and	h
		ld	h, a
Not0Freq:
		push	bc
		ld	b,h
		ld	c,l
		add	hl,hl
		ld	e, l
		ld	d, h
		add	hl,hl
		add	hl,de
		add	hl,hl	;*12
		add	hl,bc	;*13
		ld	a,b
		or	a
		rra
		ld	b,a
		ld	a,c
		rra
		ld	c,a
		add	hl,bc	;*13.5
		pop	bc
*/		
		ret
; End of function FreqAY_to_VI53
	ENDIF


;init = mus_init
;play =  trb_play
mus_stop
stop	
;			ld d,#0d			
;1:			
;			ld a,d : out (AY_REG),a
;			xor a : out (AY_DATA),a
;			dec d
;			jp nz,1b
			IFDEF PLAY_VI53
			call muteVI53
			ENDIF
			ret

mus_init0:
			ld (mus_init+1),hl
mus_init	ld hl, menuMusic
			ld (mus_init+1),hl
			ld	 a, l
			ld	 (mus_low+1), a
			ld	 a, h
			ld	 (mus_high+1), a
			ld	de, 16*4
			add	 hl, de
			ld (stack_pos+1), hl
			ld a, LD_HL_CODE
			ld (trb_play), a

			xor a
			ld hl, stack_pos
			ld (hl), a
			inc hl

			ld (trb_play+1), hl				

			IFDEF PLAY_VI53
				ld a,vCH : ld (VI1),a;
				ld a,vCH+#40 : ld (VI1),a;
				ld a,vCH+#80 : ld (VI1),a;
			ENDIF
			

			ret							
			

pause_rep	db 0
trb_pause	ld hl,pause_rep
			dec	 (hl)
			ret nz						

			ld a,(savedByte) : ld (trb_play+2),a

saved_track	
			ld hl, LD_HL_CODE			; end of pause
			ld (trb_play), hl
			ld	hl, (trb_play+1)		
			jp trb_rep					
			

endtrack	//end of track
			pop	 hl
			jp mus_init
			

pl_frame	call pl0x						
after_play_frame
			xor	 a
			ld	 (stack_pos), a				
			SAVE_POS 						
			dec	 l							
trb_rep		dec	 l
			dec (hl)
			ret	 nz							
trb_rest	
			dec	 l
			dec	 l
			ld	 (trb_play+1), hl
			ret								

mus_play					
			call trb_play
			IFDEF PLAY_VI53
			call flushVI
			ELSE
			call flushAY
			ENDIF
			ret

trb_play				
			ld hl, (stack_pos+1)
			ld a, (hl)
			add a
			jp nc, pl_frame				    
pl1x		// Process ref	
			ld b, (hl)
			inc hl
			ld c, (hl)
			inc hl
			jp p, pl10						

pl11		
			ld a, (hl)			
			inc hl	
			ex	de,hl
			ld  hl, (trb_play+1)
			dec	 l
			dec (hl)
			jp	 z, same_level_ref			
nested_ref
			// Save pos for the current nested level
			inc	 l
			ld	(hl),e
			inc	l
			ld	(hl),d
			inc  l							
same_level_ref
			ld	 (hl),a
			inc	 l
			// update nested level
			ld	 (trb_play+1),hl			

			ex	de,hl					
			add hl, bc	
			ld a, (hl)
			add a		            		
			call pl0x						
			// Save pos for the new nested level
			SAVE_POS 						
			ret							 
			

savedByte: db 0

single_pause
			pop	 de
			jp	 after_play_frame
long_pause
			inc	 hl
			ld	 a, (hl)
			inc hl
			jp	 pause_cont
pl_pause	and	 #0f
			inc hl
			jp z, single_pause
pause_cont	
			//set pause
			ld (pause_rep), a	
			//SAVE_POS
			ex	de,hl
			ld	hl, (trb_play+1)
			ld  a, l
			ld (saved_track+2), a			

			ld	(hl),e
			inc	l
			ld	(hl),d						
			
			ld a,(trb_play+2) : ld (savedByte),a
;=====================================================================		    
			ld a,JP_CODE : ld (trb_play),a
			ld hl,trb_pause : ld (trb_play+1),hl						
;=====================================================================						
			pop	 hl						
			ret								

pause_or_psg1
			add	 a
			ld a, (hl)
			jp c, pl_pause
			jp z, long_pause
		    //psg1 or end of track
			cp #0f
			jp z, endtrack
			dec a	 
			inc hl
						
;===============================================================================						
			push de : push hl
			ld hl,AYREGS : ld e,a : ld d,0 : add hl,de : ex hl,de
			pop hl
			ld a,(hl) : inc hl : ld (de),a
			pop de
;===============================================================================						
			ret								

pl00		add	 a
			jp	 nc, pause_or_psg1
			
		// psg2i
			rrca : rrca						
			
			;mEXX;exx	
			ld de,00000

mus_low		add	 0
			ld	 e, a
mus_high	adc	 0
			sub	 e
			ld	 d, a					
			ld	 a,(de)
			inc	 de
			
					
			push de
			inc	 hl							
			call reg_left_6_D5

			
			pop de
			ld	 a, (de)			
					
			add a			
			jp play_by_mask_13_6

		

pl10
			SAVE_POS 						

			ex	de,hl

			;set 6, b
			ld a,b : or 01000000b : ld b,a

			add hl,bc

			ld a, (hl)
			add a		            		
			
			call pl0x
			ld	hl, (trb_play+1)
			jp trb_rep						
			


pl0x					
			add a					
			jp nc, pl00						

pl01	// player PSG2
			inc hl
			jp z, play_all_0_5				
play_by_mask_0_5
CH = 0
			dup 5
				add a
				jp c,1f
			    ld b,a;push af
				ld a,(hl) : inc hl
				ld (AYREGS + CH),a
				ld a,b;pop af				
CH=CH+1				
1		
			edup									
			add a
			jp c, play_all_0_5_end						
			ld a,(hl) : inc hl
			ld (AYREGS + 5),a


second_mask	ld a, (hl)
			inc hl					
before_6    add a
			jp z,play_all_6_13							
			jp play_by_mask_13_6			
play_all_0_5			
			cpl						; 0->ff			
			ld a,(hl) : inc hl 			
			ld (AYREGS + 0),a			
CHN = 1
			dup 4				
				ld a,(hl) : inc hl 
				ld (AYREGS + CHN),a				
CHN = CHN + 1				
			edup					
			
			ld a,(hl) : inc hl 
			ld (AYREGS + CHN),a
				
play_all_0_5_end
			ld a, (hl)
			inc hl					
			add a
			jp nz,play_by_mask_13_6
play_all_6_13
			cpl						; 0->ff, keep flag c
			// write regs [6..12] or [6..13] depend on flag
			jp	 c, 7f				; 4+7=11	
CHL = 5			
			dup 8
CHL = CHL + 1			
				ld a,(hl) : inc hl :
				ld (AYREGS + CHL),a
			edup
			ret	
7:
CHL = 5			
			dup 7
CHL = CHL + 1			
				ld a,(hl) : inc hl :
				ld (AYREGS + CHL),a
			edup
			ret			
			

play_by_mask_13_6
			jp c,1f
			ld b,a;push af
			ld a,(hl) : inc hl 
			ld (AYREGS + 13),a
			ld a,b;pop af			
1			
			add a
			jp c,1f
			ld b,a;push af
			ld a,(hl) : inc hl : 
			ld (AYREGS + 12),a
			ld a,b;pop af			
1			

reg_left_6	add a
			jp c,1f
			ld e,a;push af
			ld a,(hl) : inc hl
			ld (AYREGS + 11),a
			ld a,e;pop af
1			
CH = 11
			dup 4
CH = CH - 1			
				add a
				jp c,1f
				ld e,a;push af
				ld a,(hl) : inc hl 				
				ld (AYREGS + CH),a
				ld a,e;pop af
1									
			edup

 			add a
			ret c	
			ld a,(hl) : inc hl 
			ld (AYREGS + CH - 1),a			
			ret		

reg_left_6_D5
			add a
			jp c,1f
			ld e,a;push af
			ld a,(hl) : inc hl
			ld (AYREGS + 5),a
			ld a,e;pop af

1			
CH=5
			dup 4
CH=CH-1			
				add a
				jp c,1f

				ld e,a;push af
				ld a,(hl) : inc hl 				
				ld (AYREGS + CH),a
				ld a,e;pop af
1									
			edup
 			add a
			ret c
CH=CH-1		
			ld a,(hl) : inc hl 
			ld (AYREGS + CH),a
			ret					

stack_pos	
			dup MAX_NESTED_LEVEL		// Make sure packed file has enough nested level here
				DB 0	// counter
				DW 0	// HL value (position)
			edup
stack_pos_end
;			ASSERT high(stack_pos) == high(stack_pos_end), Please move player code in memory for several bytes.
			;DISPLAY	"player code occupies ", /D, $-stop, " bytes"
